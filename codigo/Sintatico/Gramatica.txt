Gramática Formal do Transpilador Portugol → C#
================================================

EBNF (Extended Backus-Naur Form) alinhada ao léxico e sintático atuais

Programa ::= 'programa' '{' (DeclaracaoVar | FuncaoDecl)* '}'

Tipo ::= 'inteiro' | 'real' | 'logico' | 'caracter' | 'cadeia'

DeclaracaoVar ::= Tipo Declarador (',' Declarador)*

Declarador ::= Ident Indices? Inicializacao?

Indices ::= ('[' Expressao ']')+

Inicializacao ::= '=' (ListaInicializacao | Expressao)

ListaInicializacao ::= '{' ElementoInit (',' ElementoInit)* '}'

ElementoInit ::= ListaInicializacao | Expressao

FuncaoDecl ::= 'funcao' Tipo? Ident '(' Parametros? ')' Bloco

Parametros ::= Param (',' Param)*

Param ::= Tipo '&'? Ident

Bloco ::= '{' Instrucao* '}'

Instrucao ::= 
    | DeclaracaoVar                 
    | Escreva                       
    | Leia                          
    | Se                            
    | Enquanto                      
    | FacaEnquanto                  
    | Para                          
    | Escolha                       
    | Retorne                       
    | Pare                          
    | AtribuicaoOuChamada           
    | Bloco                         

Escreva ::= 'escreva' '(' ArgList ')'

Leia ::= 'leia' '(' Ident (',' Ident)* ')'

ArgList ::= Expressao (',' Expressao)*

Se ::= 'se' '(' Expressao ')' InstrucaoOuBloco ('senao' InstrucaoOuBloco)?

InstrucaoOuBloco ::= Instrucao | Bloco

Enquanto ::= 'enquanto' '(' Expressao ')' InstrucaoOuBloco

FacaEnquanto ::= 'faca' Bloco 'enquanto' '(' Expressao ')'

Para ::= 'para' '(' InicializacaoPara ';' Expressao ';' AtualizacaoPara ')' InstrucaoOuBloco

InicializacaoPara ::= DeclaracaoVar | AtribOuInc

AtualizacaoPara ::= AtribOuInc

Escolha ::= 'escolha' '(' Expressao ')' '{' (Caso | Padrao)* '}'

Caso ::= 'caso' Expressao ':' BlocoCasos

Padrao ::= 'caso' ('contrario' | 'padrao') ':' BlocoCasos

BlocoCasos ::= InstrucaoCasos*

Instrucao ::= 
    | DeclaracaoVar ';'
    | Escreva ';'
    | Leia ';'
    | Retorne ';'
    | Pare ';'
    | AtribuicaoOuChamada ';'
    | Se
    | Enquanto
    | FacaEnquanto
    | Para
    | Escolha
    | Bloco


Pare ::= 'pare'

Retorne ::= 'retorne' Expressao?

AtribuicaoOuChamada ::= 
    | Ident Indices? '=' Expressao
    | Ident '(' ArgList? ')'
    | Ident ('++' | '--')

AtribOuInc ::= 
    | Ident '=' Expressao
    | Ident ('++' | '--')

Expressao ::= ExprOu

ExprOu ::= ExprE ('ou' ExprE)*

ExprE ::= ExprRel ('e' ExprRel)*

ExprRel ::= ExprAdd (RelOp ExprAdd)*

RelOp ::= '==' | '!=' | '<' | '>' | '<=' | '>='

ExprAdd ::= ExprMul (('+' | '-') ExprMul)*

ExprMul ::= ExprUn (('*' | '/' | '%') ExprUn)*

ExprUn ::= 
    | '-' ExprUn
    | 'nao' ExprUn
    | '++' Ident
    | '--' Ident
    | Primaria

Primaria ::= 
    | '(' Expressao ')'
    | Numero
    | Caracter
    | String
    | ('verdadeiro' | 'falso')
    | Ident Sufixo?

Sufixo ::= 
    | Indices
    | '(' ArgList? ')'
    | ('++' | '--')

Ident ::= [A-Za-z_][A-Za-z0-9_]*

Numero ::= [0-9]+('.'[0-9]+)?

Caracter ::= '\'' (\\.|[^'\n]) '\''

String ::= '"' (\\.|[^"\n])* '"'

---

Observações de Precedência:
- Parênteses, negação (nao), unário '-', prefixo '++'/'--' — mais alta
- Multiplicação (*), divisão (/), módulo (%)
- Adição (+), subtração (-)
- Operadores relacionais (==, !=, <, >, <=, >=)
- E lógico (e)
- OU lógico (ou) — mais baixa

Palavras-chave Reservadas (case-insensitive):
programa, funcao, retorne, escreva, leia, se, senao, enquanto, faca, para,
escolha, caso, contrario, padrao, pare, verdadeiro, falso, nao

Símbolos:
( ) { } [ ] , ; : = + - * / % > < >= <= == != ++ -- &

Comentários:
// comentário de linha
/* comentário de bloco */
